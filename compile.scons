import os
import glob

env = Environment(ENV=os.environ)

thesis = env.PDF(target='thesis.pdf', source='thesis.tex')

# OmniGraffle file export builder
graffle_builder = Builder(
    action='./graffle.sh $SOURCE $TARGET',
    suffix='.pdf', src_suffix='.graffle')
env.Append(BUILDERS={'Graffle': graffle_builder})

graffle_files = []
src_prefix = '../src/'
for graffle_file in glob.glob(src_prefix + 'fig/*.graffle'):
    graffle_file = graffle_file[len(src_prefix):]
    graffle_files.extend(env.Graffle(graffle_file))

Depends(thesis, graffle_files)

# PDF figures
for pdf_file in glob.glob(src_prefix + 'fig/*.pdf'):
    Depends(thesis, pdf_file[len(src_prefix):])

env.Alias('graffle', graffle_files)
env.Alias('thesis', thesis)
env.Default(thesis)

# vim: ft=python :
