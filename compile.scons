import os


def files_with_ext(extension):
    filtered_files = []
    src_prefix = '../src/'
    for root, dirs, files in os.walk(src_prefix):
        for file in files:
            if not file.endswith(extension):
                continue
            file = os.path.join(root, file)
            filtered_files.append(file[len(src_prefix):])
    return filtered_files


env = Environment(ENV=os.environ)

thesis = env.PDF(target='thesis.pdf', source='thesis.tex')

# OmniGraffle file export builder
graffle_builder = Builder(
    action='./graffle.sh $SOURCE $TARGET',
    suffix='.pdf', src_suffix='.graffle')
env.Append(BUILDERS={'Graffle': graffle_builder})

graffle_files = [env.Graffle(file) for file in files_with_ext('.graffle')]
Depends(thesis, graffle_files)

# Other dependencies
Depends(thesis, files_with_ext('.pdf'))
Depends(thesis, files_with_ext('.tex'))
Depends(thesis, files_with_ext('.sty'))
Depends(thesis, files_with_ext('.bib'))

env.Alias('graffle', graffle_files)
env.Alias('thesis', thesis)
env.Default(thesis)

# vim: ft=python :
